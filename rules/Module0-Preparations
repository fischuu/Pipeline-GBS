if config["genome"] == "":
    print(f"No reference genome provided, skipping the star indexing step for reference genome")
else:
    rule star_create_index:
        """
        Create Genome Index for reference genome (STAR).
        """
        input:
            fasta=config["genome"],
        output:
            "%s/chrName.txt" % (config["genome-star-index"])
        log:
            "%s/logs/STAR/star_reference_index.log" % (config["project-folder"])
        benchmark:
            "%s/benchmark/STAR/star_reference_index.benchmark.tsv" % (config["project-folder"])
        threads: lambda cores: cpu_count()
        conda:"envs/star.yaml"
        singularity: config["singularity"]["star"]
        params: index=config["genome-star-index"]
        shell:"""
                STAR --runThreadN {threads} --runMode genomeGenerate --genomeDir {params.index} --genomeFastaFiles {input.fasta} 2> {log}
        """
        
rule Concatenate_lanes:
    """
    Concatenate the demultiplexed fastq files (BASH).
    """
    input:
        R1=expand("%s/FASTQ/RAW/{rawsamples}_R1_001.fastq.gz" % (config["project-folder"]), rawsamples=rawsamples),
        R2=expand("%s/FASTQ/RAW/{rawsamples}_R2_001.fastq.gz" % (config["project-folder"]), rawsamples=rawsamples)
    output:
        R1=temp("%s/FASTQ/CONCATENATED/{samples}_R1_001.merged.fastq.gz" % (config["project-folder"])),
        R1Report="%s/FASTQ/CONCATENATED/{samples}_R1_001.merged.fastq.gz.report" % (config["project-folder"]),
        R2=temp("%s/FASTQ/CONCATENATED/{samples}_R2_001.merged.fastq.gz" % (config["project-folder"])),
        R2Report="%s/FASTQ/CONCATENATED/{samples}_R2_001.merged.fastq.gz.report" % (config["project-folder"])
    log:
        "%s/logs/SHELL/catFastq_{samples}.log" % (config["project-folder"])
    benchmark:
        "%s/benchmark/SHELL/{samples}.benchmark.tsv" % (config["project-folder"])
    threads: lambda cores: cpu_count()
    params:
        infolder="%s/FASTQ/RAW" % (config["project-folder"]),
        outfolder="%s/FASTQ/CONCATENATED" % (config["project-folder"])
    shell:"""
        mkdir -p {params.outfolder}
        cat {params.infolder}/{wildcards.samples}*_R1_001.fastq.gz > {output.R1} 2> {log}
        ls {params.infolder}/{wildcards.samples}*_R1_001.fastq.gz > {output.R1Report} 2> {log}
        cat {params.infolder}/{wildcards.samples}*_R2_001.fastq.gz > {output.R2} 2> {log}
        ls {params.infolder}/{wildcards.samples}*_R2_001.fastq.gz > {output.R2Report} 2> {log}
  	"""

if config["genome"] == "":
    print(f"No reference genome provided, skipping the bwa indexing step for reference genome")
else:  	
    rule bwa_create_index:
        """
        Index the Reference Genome (BWA).
        """
        input:
            config["genome"]
        output:
            config["genome-bwa-index"]
        log:
            "%s/logs/BWA/bwa-fai-IndexReferenceGenome.log" % (config["project-folder"])
        benchmark:
            "%s/benchmark/BWA/bwaIndexReferenceGenome.benchmark.tsv" % (config["project-folder"])
        threads: lambda cores: cpu_count()
        conda:"envs/gbs.yaml"
        singularity: config["singularity"]["gbs"]
        shell:"""
                bwa index -a bwtsw {input} 2> {log}
                samtools faidx {input} 2> {log}
      	"""
